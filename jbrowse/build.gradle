import org.labkey.gradle.util.BuildUtils;

repositories {
   jcenter()
   maven {
      url "http://clojars.org/repo"
   }
}

dependencies {
   BuildUtils.addLabKeyDependency(project: project, config: "compile", depProjectPath: ":server:modules:LabDevKitModules:LDK", depProjectConfig: "apiCompile")
   BuildUtils.addLabKeyDependency(project: project, config: "compile", depProjectPath: ":server:modules:DiscvrLabKeyModules:SequenceAnalysis", depProjectConfig: "apiCompile")
   BuildUtils.addLabKeyDependency(project: project, config: "compile", depProjectPath: ":server:modules:LabDevKitModules:laboratory", depProjectConfig: "apiCompile")
}

project.task("copyJBrowse",
        type: Copy,
        group: "Build",
        description: "Copy jbrowse app into webapp directory",
        { CopySpec copy ->
           copy.from fileTree("./node_modules/@gmod/jbrowse").exclude('css/main.css')
           copy.into new File("./resources/web/jbrowseApp/")
        }
).doFirst({
    File f = project.file('./node_modules/@gmod/jbrowse');
    if (!f.exists()){
        throw new GradleException("Input source for copyJBrowse doesn't exist: " + f.getAbsolutePath())
    }
})

project.task("copyJBrowseCss",
        type: Copy,
        group: "Build",
        description: "Copy jbrowse main.css into webapp directory",
        { CopySpec copy ->
            copy.from new File("./webpack/main.css")
            copy.into new File("./resources/web/jbrowseApp/css/")
        }
).doFirst({
    File f = project.file('./resources/web/jbrowseApp/index.html');
    if (!f.exists())
    {
        throw new GradleException("copyJBrowse was not successful.  missing: " + f.getAbsolutePath())
    }

    File f2 = project.file('./resources/web/jbrowseApp/src/dijit/themes/dijit.css');
    if (!f2.exists())
    {
        throw new GradleException("copyJBrowse was not successful.  missing jbrowse dependencies: " + f2.getAbsolutePath())
    }

    // NOTE: due to bug/inconsistency in how the jbrowse postinstall script is run, several modules may not be copied to /src.  This is a check against this.
    // There's a jbrowse PR around this as well.
    File f3 = project.file('./resources/web/jbrowseApp/src/jszlib');
    if (!f3.exists())
    {
        throw new GradleException("copyJBrowse was not successful.  missing jbrowse dependencies: " + f3.getAbsolutePath())
    }
})


project.tasks.copyJBrowse.dependsOn(project.tasks.npmInstall)

project.tasks.copyJBrowse.mustRunAfter("npmRunBuild")
project.tasks.copyJBrowse.mustRunAfter("npmRunBuildProd")

project.tasks.copyJBrowseCss.dependsOn(project.tasks.copyJBrowse)
project.tasks.module.dependsOn(project.tasks.copyJBrowse)
project.tasks.module.dependsOn(project.tasks.copyJBrowseCss)
